Class {
	#name : #GAME2048Element,
	#superclass : #Game2048Elements,
	#instVars : [
		'game',
		'slotsContainer',
		'slotElementMatrix',
		'skin',
		'elementByTile',
		'tilesContainer',
		'isSlideEnabled',
		'keyBindingsEventHandler'
	],
	#category : #'Project_2048_Game_UI'
}

{ #category : #'as yet unclassified' }
GAME2048Element class >> on: aGame skin: aSkin [

	^ self basicNew
		createGrid: aGame skin: aSkin;
		yourself
]

{ #category : #'as yet unclassified' }
GAME2048Element class >> openGrid [

	| game gameElement space skin |
	game := Game2048 new.
	game gridSize: 4.
	skin := Game2048Skin new.
	gameElement := self on: game skin: skin.

	space := BlSpace new.
	space root addChild: gameElement.
	space
		extent: (game gridSize @ game gridSize ) * 80;
		show
]

{ #category : #'as yet unclassified' }
GAME2048Element >> addTileValueFor: row and: col [

	| tileElement  |
	tileElement := GAME2048LabelElement new
	text: (game grid at: row at: col) value asRopedText;
	yourself.
		
	tilesContainer addChild: tileElement.
	
	tileElement transformDo: [ :t |
		t translateBy: ((slotElementMatrix
		at: col
		at: row ) position)
		 ].

	elementByTile
		at: (col@row)
		put: tileElement.

	skin applyTileSkinOn: tileElement.

	^ tileElement
]

{ #category : #'as yet unclassified' }
GAME2048Element >> createGrid: theGame [

	game := theGame.
	
	slotsContainer layout: BlGridLayout vertical.
	slotsContainer layout rowCount: game gridSize.
	slotElementMatrix := Array2D
		rows: game gridSize
		columns: game gridSize
		tabulate: [ :row :column |
			self newSlotElementForRow: row column: column ].
	slotElementMatrix do: [:each |
		slotsContainer addChild: each ].
	
	1 to: game gridSize by: 1 do: [ :row |
		1 to: game gridSize by: 1 do: [ :col |
			(game grid at: row at: col) ifNotNil:[ 
			self addTileValueFor: row and: col]
        ]
    ].
	
]

{ #category : #'as yet unclassified' }
GAME2048Element >> createGrid: theGame skin: theSkin [

	self
		initialize;
		initializeWithSkin: theSkin;
		createGrid: theGame
]

{ #category : #'as yet unclassified' }
GAME2048Element >> initialize [

	super initialize.

	elementByTile := Dictionary new.

	"The tile container is an overlay on top of the slots."
	slotsContainer := Game2048Elements new.
	tilesContainer := Game2048Elements new.
	self
		addChild: slotsContainer;
		addChild: tilesContainer.

	"Key bindings need to be registered on the parent"
	isSlideEnabled := false.
	self
		when: BlElementAddedToParentEvent
		do: [ :anEvent |
			isSlideEnabled := true.
			self
				registerKeyBindings ]
]

{ #category : #'as yet unclassified' }
GAME2048Element >> initializeWithSkin: aSkin [

	skin := aSkin.
	skin applyGameSkinOn: self
]

{ #category : #'as yet unclassified' }
GAME2048Element >> newSlotElementForRow: row column: column [

	| newSlotElement |
	newSlotElement := self class new.
	newSlotElement id: ('{1}@{2}' format: {column.row}).

	skin applySlotSkinOn: newSlotElement.
	
	^ newSlotElement
]

{ #category : #'as yet unclassified' }
GAME2048Element >> refreshAfterMovedTile [

	1 to: game gridSize by: 1 do: [ :row |
		1 to: game gridSize by: 1 do: [ :col |
			(game grid at: row at: col) ifNotNil:[ 
			self addTileValueFor: row and: col]
        ]
    ].
]

{ #category : #'as yet unclassified' }
GAME2048Element >> registerKeyBindings [

	| selectorByKey |
	selectorByKey := {
		KeyboardKey up    -> #moveUp.
		KeyboardKey down  -> #moveDown.
		KeyboardKey left  -> #moveLeft.
		KeyboardKey right -> #moveRight.
	} asDictionary.

	keyBindingsEventHandler := BlEventHandler on: BlKeyUpEvent do: [ :evt |
		selectorByKey
			at: evt key
			ifPresent: [ :slideSelector |
				self
					slideIfEnabled: slideSelector
					andDisableDuring: 0.25 seconds ] ].

	self parent addEventHandler: keyBindingsEventHandler.
]

{ #category : #'as yet unclassified' }
GAME2048Element >> slideIfEnabled: slideSelector andDisableDuring: aDuration [

	isSlideEnabled ifFalse: [ ^self ].

	"Disable slides during a period"
	isSlideEnabled := false.
	[	self space time wait: aDuration.
		isSlideEnabled := true ] fork.
	
	game perform: slideSelector.
	
	self refreshAfterMovedTile.
]

{ #category : #'as yet unclassified' }
GAME2048Element >> slotsContainer [

	^ slotsContainer
]

{ #category : #'as yet unclassified' }
GAME2048Element >> tilesContainer [

	^ tilesContainer
]
